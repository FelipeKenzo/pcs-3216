{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Emulador de uma Máquina de Von Neumann\n",
    "\n",
    "Projeto desenvolvido para a disciplina de Sistemas de Programação da Escola Politécnica da USP.\n",
    "\n",
    "## Compilação\n",
    "\n",
    "O projeto foi desenvolvido no Ubuntu 19.10, e deve funcionar para qualquer sistema Linux. Basta executar o comando `make`\n",
    "para compilar os arquivos.\n",
    "\n",
    "## Arquitetura da Máquina de Von Neumann\n",
    "\n",
    "A Máquina de Von Neumann (MVN) implementada possui instruções de 16 *bits*, sendo os quatro primeiros reservados para o código\n",
    "de operação (opcode) e os restantes para operando da instrução. Possui apenas dois registradores, o contador de instruções\n",
    "(PC) e o acumulador (AC). O endereçamento de memória utiliza 12 *bits*, de forma que são disponíveis 4096 posições de memória\n",
    "acessíveis. Cada posição possui um *byte* de largura.\n",
    "\n",
    "As operações disponíveis são:\n",
    "\n",
    "| opcode | mnemônico | efeito |\n",
    "|--------|----------|--------|\n",
    "| 0xxx | JP  | PC = xxx\n",
    "| 1xxx | JZ  | if (AC == 0) PC = xxx\n",
    "| 2xxx | JNE | if (AC < 0) PC = xxx\n",
    "| 3xxx | LV  | AC = 0xx (último byte)\n",
    "| 4xxx | ADD | AC += mem[xxx]\n",
    "| 5xxx | SUB | AC -= mem[xxx]\n",
    "| 6xxx | MUL | AC *= mem[xxx]\n",
    "| 7xxx | DIV | AC /= mem[xxx]\n",
    "| 8xxx | LD  | AC = mem[xxx]\n",
    "| 9xxx | MM  | mem[xxx] = AC\n",
    "| Axxx | SC  | mem[xxx, xxx+1] = PC; PC = xxx+2\n",
    "| Bxxx | RS  | PC = xxx\n",
    "| Cxxx | HM  | para a execução\n",
    "| Dxxx | GD  | lê um byte do arquivo de entrada para AC\n",
    "| Exxx | PD  | escreve AC no arquivo de saída\n",
    "| Fxxx | OS  | chamada de sistema (ainda não implementada)\n",
    "\n",
    "A parte de *input/output* é feita por meio de arquivos em *plain text* dentro da pasta `/filesystem`. Por *default*, o arquivo\n",
    "de entrada é o `input.txt` e o de saída é o `output.txt`.\n",
    "\n",
    "### O Motor de Eventos\n",
    "\n",
    "A MVN foi implementada utilizando o conceito de um motor de eventos. Isto é, em um loop de simulação, a MVN busca uma instrução na memória, decodifica sua operação e operandos e ativa os procedimentos específicos de reação para a simulação das intstruções correspondentes. Ao final de um ciclo, atualiza o ponteiro (*program counter*) para o endereço da prócima instrução a ser executada.\n",
    "\n",
    "## Utilização\n",
    "\n",
    "A interface com o usuário é feita por meio de uma interface de linha de comando (CLI):\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div style=\"background:black\">\n",
    "<code style=\"background:black;color:white\">\n",
    "__     __          _   _                                         __     ____  __ \n",
    "\\ \\   / /__  _ __ | \\ | | ___ _   _ _ __ ___   __ _ _ __  _ __   \\ \\   / /  \\/  |\n",
    " \\ \\ / / _ \\| '_ \\|  \\| |/ _ \\ | | | '_ ` _ \\ / _` | '_ \\| '_ \\   \\ \\ / /| |\\/| |\n",
    "  \\ V / (_) | | | | |\\  |  __/ |_| | | | | | | (_| | | | | | | |   \\ V / | |  | |\n",
    "   \\_/ \\___/|_| |_|_| \\_|\\___|\\__,_|_| |_| |_|\\__,_|_| |_|_| |_|    \\_/  |_|  |_|\n",
    "\n",
    "<div style=\"background:black\">                                                                     --Ver. 1.0--\n",
    "\n",
    "Type 'help' for available commands.\n",
    "\n",
    "~ \\$\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Os comandos disponíveis estão apresentados a seguir. São descritos em maior detalhe por meio do comando `help`.\n",
    "\n",
    "| comando | efeito |\n",
    "|---------|--------|\n",
    "| `assemble [src] [-w] [-o out]` | monta um arquivo-fonte em assembly para um arquivo de listagem e um em código-objeto.\n",
    "| `exit` | sai do programa.\n",
    "| `help` | imprime a mensagem de ajuda.\n",
    "| `load [program]` | carrega um programa em código-objeto para a memória da MVN.\n",
    "| `ls` | lista os arquivos na pasta `/filesystem`.\n",
    "| `print [file]`| imprime o conteúdo de um arquivo.\n",
    "| `md [address] [range]` | imprime o conteúdo da memória da MVN em *chunks* de 16 posições seguidas.\n",
    "| `mm [address] [data]` | modifica o conteúdo da memória da MVN.\n",
    "| `rm [file(1)]...[file(n)]` | remove um ou mais arquivos da pasta `/filesystem`\n",
    "| `run [address]` | aciona a execução da MVN. É possível especificar o endereço inicial.\n",
    "| `set [register] [data]` | modifica o conteúdo de um registrador (AC ou PC).\n",
    "| `status` | imprime o conteúdo dos registradores da MVN.\n",
    "| `step [address]` | executa as instruções da MVN passo-a-passo, pressionando `enter`.\n",
    "| `turn [on off]` | liga ou desliga a MVN. |\n",
    "\n",
    "### Assembler\n",
    "\n",
    "O *assembler* da MVN foi escrito em alto-nível. Trata-se de um montador **absoluto**, isto é, todas as posições de memória\n",
    "já estão definidas. Para utilizá-lo, basta digitar o comando `assemble [src]` na linha de comando do programa, em que \n",
    "`[src]` é um arquivo-fonte em *assembly* localizado no diretório `/filesystem`.\n",
    "\n",
    "A estrutura de um programa típico da MVN é descrita a seguir:"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div style=\"background:#F0F0F0\">\n",
    "<code style=\"background:#F0F0F0;color:black\">\n",
    "; Comentários iniciam-se com ';'\n",
    "\n",
    "        ORG 100  ; Posição da primeira instrução\n",
    "        \n",
    "START:  ADD $120  ; Labels terminam com ':'\n",
    "        SUB 440   ; Operandos podem estar tanto em decimal quanto em hexadecimal\n",
    "        MUL VAR1  ; Operandos podem ser outras labels também\n",
    "        GD        ; Tanto GD quanto\n",
    "        PD        ; PD nâo recebem operandos.\n",
    "        ...\n",
    "        JP  START\n",
    "        \n",
    "        ORG 120  ; Posição da próxima instrução\n",
    "        \n",
    "VAR1:   CON $F2   ; Constantes só possuem um byte de tamanho\n",
    "        END START ; Fim do programa\n",
    "        MM  VAR1  ; Não será processado pelo assembler\n",
    "    \n",
    "</code>\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Note que, além das instruções de máquina da MVN, existem três pseudo-instruções do assembler:\n",
    "\n",
    "| mnemônico | efeito |\n",
    "| --------- | ------ |\n",
    "| ORG | Modifica o endereço das próximas instruções. |\n",
    "| CON | Define uma constante de um byte. |\n",
    "| END | Indica ao *assembler* que o programa acabou. |\n",
    "\n",
    "Os endereços disponíveis para os programas do usuário iniciam-se na posição `$070` (ver *loader*)\n",
    "\n",
    "### Loader\n",
    "\n",
    "O *loader* foi implementado em linguagem de máquina, e ocupa os primeiros 148 bytes da memória da MVN. Ele é injetado pela\n",
    "interface na inicialização da MVN. Para utilizá-lo, basta digitar o comando `load [program]` na linha de comando do programa,\n",
    "em que `[program]` é um código-objeto no formato '.vnc'.\n",
    "\n",
    "O formatao '.vnc' nada mais é do que um arquivo em *plain-text*, em que os dados são escritos em \"hexadecimal\". Os dados a\n",
    "serem escritos na memória da MVN são divididos em blocos, cuja estrutura é:"
   ]
  },
  {
   "cell_type": "raw",
   "metadata": {},
   "source": [
    "[origem][tamanho][     dados    ][checksum]\n",
    "   2*       1*         1-15*         1*\n",
    "   \n",
    "* Tamanho em bytes (cada byte são dois dígitos hexadecimais)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "O programa-fonte do *loader* encontra-se a seguir:"
   ]
  },
  {
   "cell_type": "raw",
   "metadata": {},
   "source": [
    "; LOADER (starts at $000)\n",
    "            ld      fail        \n",
    "            sub     fail\n",
    "            mm      fail        ; Resets fail flag\n",
    "            jp      readAddr    ; Jumps data area to first instruction\n",
    "\n",
    "; //---------Data Area----------//\n",
    "\n",
    "counter:    con     $00         ; Counts number of bytes of data to be read\n",
    "one:        con     $01         ; 1 constant\n",
    "opcode:     con     $90         ; MM opcode value\n",
    "fail:       con     $00         ; If a chacksum failed\n",
    "checksum:   con     $00         ; Calculated Checksum\n",
    "compAux:    con     $FF         ; Auxiliar to calculate 1's complement\n",
    "\n",
    "; //--------Main Program--------//\n",
    "\n",
    "; Reads base address:\n",
    "readAddr:   ld      checksum    ; Loads checksum.\n",
    "            sub     checksum    ; Zero.\n",
    "            mm      checksum    ; Reset checksum.      \n",
    "            sc      readByte    ; Reads first address byte from file\n",
    "            add     opcode      ; Adds upper address to opcode\n",
    "            mm      write_1     ; Writes result to write_1\n",
    "            sc      readByte    ; Reads second address byte from file\n",
    "            mm      write_2     ; Writes to write_2\n",
    "\n",
    "; If the read address was zero, we end the program:\n",
    "            jz      zero1       ; If the last byte was zero, we check the upper byte.\n",
    "            jp      readSize    ; Else, we continue to read the size byte.\n",
    "zero1:      ld      write_1     ; Loads upper byte (OP + upper address).\n",
    "            sub     opcode      ; Removes opcode.\n",
    "            jz      eop         ; If it is also zero, we terminate the program.\n",
    "\n",
    "readSize:   sc      readByte    ; Reads byte count from file\n",
    "            mm      counter     ; Writes byte count to counter\n",
    "\n",
    "; Memory writing loop\n",
    "loop:       sc      readByte    ; Reads a byte of data\n",
    "write_1:    con     $90         ; MM first byte (will be read as instruction)\n",
    "write_2:    con     $00         ; MM second byte (will be read as instruction)\n",
    "            ld      write_2     ; Loads MM second byte (lower address byte)\n",
    "            add     one         ; write_2 += 1\n",
    "            mm      write_2     ; Writes new lower address byte\n",
    "            jz      addUpper    ; if equals zero (overflow) we add one to upper byte.  \n",
    "write_3:    ld      counter     ; Loads value from counter\n",
    "            sub     one         ; Counter--\n",
    "            mm      counter     ; Writes new value of counter\n",
    "            jz      checkChks   ; Compares checksums if counter = 0\n",
    "            jp      loop        ; Goes back to loop if counter != 0\n",
    "\n",
    "; Adds one to upper MM byte\n",
    "addUpper:   ld      write_1     ; Loads upper MM byte.\n",
    "            add     one         ; Write_1++\n",
    "            mm      write_1     ; Writes result.\n",
    "            jp      write_3     ; Continues back to loop\n",
    "\n",
    "; Compares the checksums\n",
    "checkChks:  ld      compAux     ; Loads $FF\n",
    "            sub     checksum    ; One's complement to calculate checksum\n",
    "            mm      checksum    ; Saves computed checksum to memory\n",
    "            gd                  ; Reads checksum from file\n",
    "            sub     checksum    ; Compares both checksums\n",
    "            jz      readAddr    ; If they are different, we continue and read next address.\n",
    "            mm      fail        ; If they are differetn, we raise error flag and terminate program.\n",
    "\n",
    "; Terminates program\n",
    "eop:        hm      readAddr    ; Halts machine.\n",
    "\n",
    "; //--------Subroutines---------//\n",
    "\n",
    "; Gets a byte and adds to checksum.\n",
    "readByte:   jp      $000        ; Return address.\n",
    "            gd                  ; Reads a byte of data.     \n",
    "            mm      data        ; Saves read byte.\n",
    "            add     checksum    ; Adds to checksum.\n",
    "            mm      checksum    ; Saves results to checksum variable.\n",
    "            ld      data        ; Recovers data that was read.\n",
    "            rs      readByte    ; Returns from soubroutine.\n",
    "; ReadByte data\n",
    "data:       con     $00         ; Data that was read."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
